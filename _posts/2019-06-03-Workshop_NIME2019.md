---
layout: post
title: COSMO Workshop at NIME2019
---


## Introduction to effect processing with Csound and the cosmo-dsp library


[![COSMO - Csound On Stage Music Operator](http://cosmoproject.github.io/images/COSMO-box-small.png)](https://vimeo.com/183101272 "COSMO - Csound On Stage Music Operator")

**Table of contents**

* TOC
{:toc}

### What is Csound?


[Csound](https://csound.com/) is an audio software and programming language for sound synthesis and composition. It was developed by Barry Vercoe at MIT Media Lab in 1986, but its roots go all the way back to the Music Software developed by Max Matthews at Bell Labs from 1957 and onwards. 

Today Csound is developed by a group of core developers with a wider community of volunteers contributing. One of the main principles in Csound development is to guarantee backwards compatibility. You can still run Csound source files from 1986 on the latest Csound release.

Although Csound has a strong tradition as a tool for composing electro-acoustic pieces and traditionally has been used in a non-interactive score driven context, nowadays it is mostly used in a real-time context. Csound can run on various different hardware and software platforms including all major operating systems as well as Android and iOS. Csound can also be called through other programming languages such as Python, Lua, C/C++, Java, etc. and is now also supported on the [Bela](https://bela.io/) platform!


### Writing a Csound instrument
Csound documents are simple text documents starting with the XML type tag ```<CsoundSynthesizer>``` and are then structured into 3 sections:  ```<CsOptions>```, ```<CsInstruments>``` and ```<CsScore>```

```
<CsoundSynthesizer>

<CsOptions>
</CsOptions>

<CsInstruments>
</CsInstruments>  

<CsScore>
</CsScore>

</CsoundSynthesizer>
```

Each section of Csound code has its on [syntax and grammar](https://csound.com/get-started.html). The first couple of lines in ```<CsOptions>``` are mainly some [hardware/software settings](https://csound.com/docs/manual/Using.html), which for now we can ignore.

Where the fun starts is in the ```<CsInstruments>``` section with defining instruments (or setting up patches). An new instrument is defined between the ```instr``` and ```endin``` lines (see example below). Instruments are built by combining signal processing modules which are called *opcodes* in Csound. There are [thousands of opcodes](https://csound.com/docs/manual/PartOpcodesOverview.html) available for different kinds of oscillators, filters, envelopes, delays, reverbs, sample manipulation tools and so on. The [Csound Reference Manual](https://csound.com/docs/manual/PartReference.html) provides more information and examples for each available opcode.

#### A very simple Csound instrument

Here is a complete example of a very simple Csound instrument which takes in audio from the sound card and passes it back out

	<CsoundSynthesizer>
	<CsOptions>
    -iadc0 ; Set up audio in
	-odac0 ; Set up audio out
    -b128 ; Buffer size
	</CsOptions>
	<CsInstruments>
	ksmps  	= 32 ; Vector size
	0dbfs	= 1 ; Set 0dbfs reference to 1
	nchnls 	= 2 ; Number of audio channels

	instr 1 ; Start a definition of instrument with number 1

		aL, aR ins  ; Opcode for getting stereo sound in
		outs aL, aR ; Opcode for sending stereo sound out
        
	endin ; End instrument definition

	</CsInstruments>
	<CsScore>
    ; Set instrument 1 to run for 86400 seconds (24 hours)
	i1 0 86400
	</CsScore>
	</CsoundSynthesizer>

#### Building a simple effect processor using the COSMO dsp-library

The [cosmo-dsp repository](http://github.com/cosmoproject/cosmo-dsp) comprises a library of ready-made audio effects (e.g Reverb, Delay, Distortion, Filters etc.). All effects are set up as independendant modules that can be combined to a custom setup of effects. To make it as much plug and play as possible, all arguments are also normalized (0-1) and scaled properly inside the effect so that the user can use them immediately.

To use one of the readymade effects, you first need to download the file containing the desired effect, place it with your csd file and include it using ```#include``` like this:

	#include "Reverb.udo"

Alternatively you can download the whole repository and include all the effects like this:

    #include "cosmo-dsp/DSP-Library/Includes/cosmo-dsp.inc"

The name and a quick explanation of the arguments can be found in the header of each ```.udo``` file in the COSMO dsp-library (e.g. [```Reverb.udo``` ](https://github.com/cosmoproject/cosmo-dsp/blob/master/DSP-Library/Effects/Reverb.udo)). A full list of all the effects available and their arguments can be found at [https://github.com/cosmoproject/cosmo-dsp/blob/master/DSP-Library/README.md](https://github.com/cosmoproject/cosmo-dsp/blob/master/DSP-Library/README.md)

To get control data from a MIDI controller (CC), we first need to add the ```-M``` flag in the ```<CsOptions>``` section:

    <CsOptions>
	-Ma
	</CsOptions>

We can either specify a specific MIDI device by using number or, like here, just use ```a``` to open all available MIDI devices

To read in actual MIDI control data, we can use the ```ctrl7``` opcode:

    kMIDI_pot ctrl7 1, 64, 0, 1

The first argument is MIDI channel, second the CC number, third minimum value and fourth maximum value. If you don't know these values, use a MIDI monitor - see [Links section](#Links).

Since all arguments are normalized between 0 and 1, we're setting the maximum value to be 1 instead of 127, which is the normal MIDI maximum value. Here is a complete example of a Reverb effect where the dry/wet mix is controlled by a MIDI controller on channel 1 and CC 64.

	<CsoundSynthesizer>
	<CsOptions>
	-iadc0 -odac0 -Ma -b128
	</CsOptions>
	<CsInstruments>
	ksmps  	= 32
	0dbfs	= 1
	nchnls 	= 2

	#include "cosmo-dsp/DSP-Library/Includes/cosmo-dsp.inc"

	instr 1 
		aL, aR ins

		kMix ctrl7 1, 64, 0, 1

		; Reverb arguments: decay, cutoff, mix
		aL, aR Reverb aL, aR, 0.9, 0.5, kMix 

		outs aL, aR
	endin


	</CsInstruments>
	<CsScore>
	i1 0 86400
	</CsScore>
	</CsoundSynthesizer>


The same approach applies to any of the other effects found at [https://github.com/cosmoproject/cosmo-dsp/tree/master/DSP-Library/Effects](https://github.com/cosmoproject/cosmo-dsp/tree/master/DSP-Library/Effects) and you can combine them in any order.

#### Building a MIDI controlled synthesizer

One thing where Csound, arguably, outperforms other environments is how quick you can write a MIDI synthesizer. This comes in very handy in combination with Bela, as you can connect any class-compliant MIDI-keyboard to the USB-port of Bela, load a Csound instrument and play it like a synth.

In ```<CsOptions>```, you can select the MIDI-device you'd like to control Csound with. As a default, the MIDI channel defines the instrument number which is called for each pressed key, this could be changed by setting [massign](https://csound.com/docs/manual/massign.html).
To avoid cpu overload by triggering too many voices in parallel, the [maxalloc](https://csound.com/docs/manual/maxalloc.html) option allows to restrict the maximum number of voices (in this example set to 4 voices).

```
<CsoundSynthesizer>
<CsOptions>
-Ma -odac0
</CsOptions>
<CsInstruments>

; Initialize global variables.
sr = 44100
ksmps = 32
nchnls = 2
0dbfs = 1

seed 0 ; init random function
maxalloc 1, 4 ; restrict to maximum 4 voices

;------------------------------------------------------
; Instrument will be played by MIDI notes on Channel 1
;------------------------------------------------------
instr 1
	iFreq cpsmidi
	iAmp ampmidi 0.3

	; Three detuned sawtooth oscillators
	aOut1 = vco2(iAmp, iFreq)
	aOut2 = vco2(iAmp, iFreq*1.004)
	aOut3 = vco2(iAmp, iFreq*0.995)

	; Envelope
	aOut = aOut1 + aOut2*0.5 + aOut3*0.5
	aEnv linsegr 0, 0.01, 0.1, 0.2, 0

	; Filter LFO and envelope
	kLFO = lfo(50, 1.2, 0)
	aFiltEnv linsegr 100, 1.2, 1000, 0.2, 0

	; Korg35 resonant low-pass filter
	aOut = K35_lpf(aOut, aFiltEnv+kLFO, 9.4, 0, 1)

	; Voice panning
	iPan random 0.2, 0.8;
	aOutL, aOutR pan2 aOut, iPan

	; Output
	outs aOutL*aEnv, aOutR*aEnv
endin


</CsInstruments>
<CsScore>
</CsScore>
</CsoundSynthesizer>
```

### Links

* Csound download page: [https://csound.com/download.html](https://csound.com/download.html)
* Csound reference manual: [https://csound.com/docs/manual/index.html](https://csound.com/docs/manual/index.html)
* **cosmo-dsp** library: [https://github.com/cosmoproject/cosmo-dsp](https://github.com/cosmoproject/cosmo-dsp)
* MIDI monitor for OS X: [https://www.snoize.com/MIDIMonitor/](https://www.snoize.com/MIDIMonitor/)
* MIDI monitor for Win and OS X: [https://www.morson.jp/pocketmidi-webpage/](https://www.morson.jp/pocketmidi-webpage/)
* Csound FLOSS manual: [http://floss.booktype.pro/csound/preface/](http://floss.booktype.pro/csound/preface/)